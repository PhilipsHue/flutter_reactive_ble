group 'com.signify.hue.flutterreactiveblelib'
version '1.0-SNAPSHOT'

buildscript {
    ext.detekt_version = '1.14.1'
    ext.kotlin_version = '1.4.21'
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.14'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.7.0.0"
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-android'
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion 30
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        main {
            proto {
                srcDir '../../flutter_reactive_ble_platform_interface/protos/'
            }
        }
    }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        consumerProguardFiles 'proguard-rules.txt'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

detekt {
    version = detekt_version
    input = files("src/main/kotlin")
    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.13.0'
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.polidea.rxandroidble2:rxandroidble:1.11.1'
    implementation 'com.google.protobuf:protobuf-javalite:3.11.0'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.5.1"
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation "com.google.truth:truth:1.0"
}
